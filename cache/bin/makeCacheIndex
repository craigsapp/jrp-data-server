#!/usr/bin/perl
#
# Programmer:    Craig Stuart Sapp <craig.stanford.edu>
# Creation Date: Wed Dec 13 09:09:27 AM PST 2023
# Last Modified: Wed Dec 13 09:14:46 AM PST 2023
# Filename:      cache/bin/makeIndex
# Syntax:        perl 5
# vim:           ts=3
#
# Description:   Reads the files in ../kern directory to create an index of MD5
#                mappings to JRP IDs.
#

use strict;

my $md5sum = `which md5 2> /dev/null`;
chomp $md5sum;
if ($md5sum =~ /^\s*$/) {
	$md5sum = `which md5sum`;
	chomp $md5sum;
}
die "Cannot find md5 or md5sum program." if $md5sum =~ /^\s*$/;

my $srcdir = "../kern";

my @files = sort glob("$srcdir/*.krn");

my $index = generateIndex(@files);

print $index;

exit(0);

###########################################################################


##############################
##
## generateIndex --
##

sub generateIndex {
	my @files = @_;
	my $output = "";
	$output .= "!! Cache index, meaning of each column:\n";
	$output .= "!! **md5   == First eight digits of MD5 checksum used to cache file.\n";
	$output .= "!! **jrpid == JRP ID (Start of file name up to first dash).\n";
	$output .= "**md5\t**jrpid\n";
	for (my $i=0; $i<@files; $i++) {
		my $file = $files[$i];
		my $base = $file;
		$base =~ s/.*\///;
		$base =~ s/\.krn$//;
		my $shortbase = $base;
		$shortbase =~ s/-.*//;

		my $sum = `$md5sum $file`;
		if ($sum =~ /([0-9a-f]{32})/) {
			$sum = $1;
		} else {
			die "Problem with MD5 extraction: $sum\n";
		}
		$sum =~ /^(.{8})/;
		my $shortsum = $1;

		$output .= "$shortsum\t$shortbase\n";
	}
	$output .= "*-\t*-\n";
	return $output;
}


