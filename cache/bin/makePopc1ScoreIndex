#!/usr/bin/perl
#
# Programmer:    Craig Stuart Sapp <craig.stanford.edu>
# Creation Date: Sun 19 Sep 2021 05:40:13 AM PDT
# Last Modified: Wed 24 Nov 2021 05:56:16 AM PST
# Filename:      cache/bin/makePopc1ScoreIndex
# Syntax:        perl 5
# vim:           ts=3
#
# Description:   Reads cache-index.hmd to generate a list of browse search fields
#                for POPC-1 scores (Chopin First Editions).
#

use strict;
use Getopt::Long;


my $notecount = `which notecount`;
chomp $notecount;
die "Cannot find notecount tool\n" if $notecount =~ /^\s*$/;


my $indexfile;
my $kerndir;
Getopt::Long::Configure("bundling");
GetOptions (
   'i|index=s' => \$indexfile
);

$indexfile = "cache-index.hmd" if $indexfile =~ /^\s*$/;

# Cache depth (number of subdirectory levels until file directories).
my $cacheDepth = 1;

my %index = getIndexFileId($indexfile);

# Sort by fileId:
my @keys = sort keys %index;

my $output = "";
# $output .= "\@\@TYPE:notecount:Integer\n\n";
foreach my $key (@keys) {
	next if $key =~ /^pl-/;   # ignore for POPC1 scores if from Poland
	next if $key =~ /^s-/;    # ignore for POPC1 scores if from Sweden
	next if $key =~ /^d-/;    # ignore for POPC1 scores if from Germany
	next if $key =~ /^f-/;    # ignore for POPC1 scores if from France
	next if $key =~ /^i-/;    # ignore for POPC1 scores if from Italy
	next if $key =~ /^sk-/;   # ignore for POPC1 scores if from Slovakia
	next if $key =~ /^cz-/;   # ignore for POPC1 scores if from Chechia
	print STDERR "Processing $key ... for score index\n";
	$output .= prepareEntry($key, $index{$key});
}
print $output;

exit(0);

###########################################################################


##############################
##
## prepareEntry --
##

sub prepareEntry {
	my ($fileid, $md5) = @_;
	my $sourcedir = getSourceDir($md5, $cacheDepth);
	my $kernfile = "$sourcedir/$md5.krn";
	die "Cannot find $sourcedir/$md5.krn\n" if !-r $kernfile;
	my %references = getBibliographicReferences($kernfile);
	my $composer = $references{"COM"};
	$composer = $references{"COM1"} if $composer =~ /^\s*$/;
	$composer = $references{"COA"} if $composer =~ /^\s*$/;
	$composer = $references{"COA1"} if $composer =~ /^\s*$/;
	$composer = $references{"COS"} if $composer =~ /^\s*$/;
	$composer = $references{"COS1"} if $composer =~ /^\s*$/;
	# die "No composer field in file $kernfile\n" if $composer =~ /^\s*$/;
	$composer = "Chopin, Fryderyk" if $composer =~ /^\s*$/;

	# my $title = "";
	my $GTL = $references{"GTL"};
	my $OPR = $references{"OPR"};
	my $OTL = $references{"OTL"};
	my $AGN = $references{"AGN"};
	my $CNT = $references{"CNT"};
	my $edit = $references{"edit"};
	my $notecount = getNoteCount($kernfile);
	my $shelfmark = $references{"NIFC-shelfmark"};
	my $siglum = $references{"SMS-siglum"};
	if ($shelfmark =~ /^\s*([^\s]+)\s+([^\s]+.*)\s*$/) {
		$siglum = $1;
		$shelfmark = $2;
	}
	my $shelfwork = $references{"SMS-shelfwork"};
	#my $fileid = $references{"!SEGMENT"};
	#$fileid =~ s/\.krn//;
	#$fileid =~ s/_.*//;

	# $CNT = "undetermined" if $CNT =~ /^\s*$/;
	$CNT = "Polish; French" if $CNT =~ /^\s*$/;
	$AGN = "uncategorized" if $AGN =~ /^\s*$/;

	my $output = "";
	$output .= "\@\@BEGIN:\tENTRY\n";
	$output .= "\@cenid:\n"; # no cenid for POPC1
	$output .= "\@fileid:\t$fileid\n";
	$output .= "\@nifcid:\n";
	$output .= "\@siglum:\t$siglum\n";
	$output .= "\@shelfmark:\t$shelfmark\n";
	$output .= "\@shelfwork:\t$shelfwork\n";
	$output .= "\@COM:\t\t$composer\n";
	$output .= "\@CNT:\t\t$CNT\n";
	$output .= "\@GTL:\t\t$GTL\n" if $GTL !~ /^\s*$/;
	$output .= "\@OPR:\t\t$OPR\n" if $OPR !~ /^\s*$/;
	$output .= "\@OTL:\t\t$OTL\n" if $OTL !~ /^\s*$/;
	$output .= "\@AGN:\t\t$AGN\n";
	$output .= "\@notecount:\t$notecount\n";
	$output .= "\@lastedit:\t$edit\n";
	$output .= "\@\@END:\t\tENTRY\n";
	$output .= "\n";

	return $output;
}



##############################
##
## getNoteCount -- Return the printed notecount for the given file.
##

sub getNoteCount {
	my ($filename) = @_;
	my $command = "$notecount -s $filename";
	my @data = `$command`;
	my $count = 0;
	if (@data != 1) {
		$count = 0;
	} else {
		my @fields = split(/\t+/, $data[0]);
		$count = $fields[1];
	}
	return $count;
}



##############################
##
## getBibliographicReferences --
##

sub getBibliographicReferences {
	my ($kernfile) = @_;
	open(FILE, $kernfile) or die "Cannot read $kernfile\n";
	my @contents = <FILE>;
	close FILE;
	my %output;
	my @dates;
	for (my $i=0; $i<@contents; $i++) {
		my $line = $contents[$i];
		next if $line !~ /^!!!([^:]+)\s*:\s*(.*)\s*$/;
		my $key = $1;
		my $value = $2;
		$output{$key} = $value;
		if ($key eq "EST") { $dates[@dates] = $value; }
		if ($key eq "END") { $dates[@dates] = $value; }
		if ($key eq "EEV") { $dates[@dates] = $value; }
	}

	my @sdates;
	for (my $i=0; $i<@dates; $i++) {
		my $value = $dates[$i];
		if ($value =~ /\b(\d{4})\b.*?\b(\d+)\b.*?\b(\d+)\b/) {
			my $year  = $1;
			my $month = int($2);
			my $day   = int($3);
			$month    = "0$month" if $month < 10;
			$day      = "0$day" if $day < 10;
			$value = "$year$month$day";
			$sdates[@sdates] = $value;
		}
	}
	@sdates = sort @sdates;
	if (@sdates > 0) {
		$output{"edit"} = $sdates[$#sdates];
	} else {
		$output{"edit"} = 0;
	}

	return %output;
}



##############################
##
## getSourceDir --
##

sub getSourceDir {
	my ($md5, $cacheDepth) = @_;
	return $md5 if $cacheDepth == 0;
	my @pieces = split(//, $md5);
	my $output = "";
	for (my $i=0; $i<$cacheDepth; $i++) {
		$output .= $pieces[$i] . "/";
	}
	$output .= $md5;
	return $output;
}



##############################
##
## getIndexFileId -- Mapping from FileId to MD5 ID.
##

sub getIndexFileId {
	my ($indexfile) = @_;
	open(FILE, $indexfile) or die "Cannot read $indexfile\n";
	my @contents = <FILE>;
	chomp @contents;
	close FILE;
	my $md5Index    = -1;
	my $fileidIndex = -1;
	my $exinterp    = 0;
	my %output;
	for (my $i=0; $i<@contents; $i++) {
		my $line = $contents[$i];
		next if $line =~ /^!/;
		if ($line =~ /^\*\*/) {
			$exinterp = 1;
			my @fields = split(/\t+/, $line);
			for (my $j=0; $j<@fields; $j++) {
				$md5Index    = $j if $fields[$j] eq "**md5";
				$fileidIndex = $j if $fields[$j] eq "**fileid";
			}
			next;
		}
		next if $line =~ /^\*/;
		next if !$exinterp;
		die "Cannot find MD5 column in index file\n" if $md5Index < 0;
		die "Cannot find FileId column in index file\n" if $fileidIndex < 0;
		my @fields = split(/\t+/, $line);
		my $fileid = $fields[$fileidIndex];
		next if $fileid eq ".";
		my $md5 = $fields[$md5Index];
		$output{$fileid} = $md5;
	}
	return %output;
}



